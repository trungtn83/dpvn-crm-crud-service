plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.5'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'com.diffplug.spotless' version '6.25.0'
    id 'maven-publish'
}

group = 'com.dpvn'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

repositories {
    mavenLocal()
    mavenCentral()
}

ext {
    set('springCloudVersion', "2023.0.1")
}

dependencies {
    implementation 'com.dpvn:shared-core:0.0.1-SNAPSHOT'
    implementation 'com.dpvn:shared-jpa:0.0.1-SNAPSHOT'
    implementation 'com.dpvn:shared-kafka:0.0.1-SNAPSHOT'
    implementation 'com.dpvn:dpvn-report-crud-service:0.0.1-SNAPSHOT:domain'
    implementation 'com.dpvn:dpvn-kiotviet-service:0.0.1-SNAPSHOT:domain'
    implementation 'com.dpvn:dpvn-gateway-service:0.0.1-SNAPSHOT:domain'
    implementation 'com.dpvn:dpvn-storage-service:0.0.1-SNAPSHOT:domain'

    runtimeOnly 'org.postgresql:postgresql'

    // MapStruct phải có ở mọi nơi vì annotationProcessor không gióng api, ko kế thừa được
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

spotless {
    java {
        target '**/*.java'
        googleJavaFormat("1.22.0")
        importOrder()
        toggleOffOn('/*', '*/')
    }
}

tasks.withType(JavaCompile) {
    options.fork = true
    options.forkOptions.jvmArgs += [
            '--add-exports', 'jdk.compiler/com.sun.tools.javac.api=ALL-UNNAMED',
            '--add-exports', 'jdk.compiler/com.sun.tools.javac.file=ALL-UNNAMED',
            '--add-exports', 'jdk.compiler/com.sun.tools.javac.parser=ALL-UNNAMED',
            '--add-exports', 'jdk.compiler/com.sun.tools.javac.tree=ALL-UNNAMED',
            '--add-exports', 'jdk.compiler/com.sun.tools.javac.util=ALL-UNNAMED'
    ]
}

task domainJar(type: Jar) {
    archiveClassifier.set('domain')
    from(sourceSets.main.output) {
        include 'com/dpvn/crmcrudservice/domain/dto/**'
        include 'com/dpvn/crmcrudservice/domain/constant/**'
    }
    manifest {
        attributes(
                'Main-Class': 'com.dpvn.crmcrudservice.CrudServiceApplication'
        )
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact domainJar
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

bootJar {
    if (project.hasProperty('jarName')) {
        archiveBaseName = project.getProperty('jarName')
        archiveVersion = '' // Set version to empty to exclude version from the JAR name
    }
    manifest {
        attributes(
                'Implementation-Title': archiveBaseName,
                'Implementation-Version': archiveVersion
        )
    }
}
